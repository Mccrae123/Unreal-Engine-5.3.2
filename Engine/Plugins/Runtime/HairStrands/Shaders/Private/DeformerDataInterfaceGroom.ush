// Copyright Epic Games, Inc. All Rights Reserved.

#include "/Plugin/ComputeFramework/Private/ComputeKernelCommon.ush"
#include "/Engine/Private/HairStrands/HairStrandsVertexFactoryCommon.ush"

///////////////////////////////////////////////////////////////////////////////////////////////////
// Bindings

uint  {DataInterfaceName}_NumControlPoints;
uint  {DataInterfaceName}_NumCurves;
float {DataInterfaceName}_VF_HairRadius;
float {DataInterfaceName}_VF_HairLength;
Buffer<uint4>  {DataInterfaceName}_PositionBuffer;
Buffer<float2> {DataInterfaceName}_Attribute0Buffer;
Buffer<uint>   {DataInterfaceName}_Attribute1Buffer;
Buffer<uint>   {DataInterfaceName}_CurveBuffer;

///////////////////////////////////////////////////////////////////////////////////////////////////
// Helpers

FHairControlPoint ReadControlPointData(uint VertexIndex)
{
	const uint MaxVertices = {DataInterfaceName}_NumControlPoints;
	if (VertexIndex >= MaxVertices)
	{
		return (FHairControlPoint)0;
	}

	// Hair deformer work in local space so no need for position offset
	return UnpackHairControlPoint(
		{DataInterfaceName}_PositionBuffer[VertexIndex], 
		float3(0, 0, 0) /*InVF_PositionOffset*/,
		{DataInterfaceName}_VF_HairRadius, 
		1.0f /*RootScale*/, 
		1.0f /*TipScale*/);
}

FHairAttributes ReadControlPointAttributeData(uint VertexIndex)
{
	const uint MaxVertices = {DataInterfaceName}_NumControlPoints;
	if (VertexIndex >= MaxVertices)
	{
		return (FHairAttributes)0;
	}

	return UnpackHairAttribute(
		{DataInterfaceName}_Attribute0Buffer[VertexIndex],
		{DataInterfaceName}_Attribute1Buffer[VertexIndex],
		{DataInterfaceName}_VF_HairLength);
}

FHairCurve ReadCurveData(uint CurveIndex)
{
	const uint MaxCurve = {DataInterfaceName}_NumCurves;
	if (CurveIndex >= MaxCurve)
	{
		return (FHairCurve)0;
	}

	return UnpackHairCurve({DataInterfaceName}_CurveBuffer[CurveIndex]);
}

///////////////////////////////////////////////////////////////////////////////////////////////////
// Interface

uint ReadNumControlPoints_{DataInterfaceName}()
{
	return {DataInterfaceName}_NumControlPoints;
}

uint ReadNumCurves_{DataInterfaceName}()
{
	return {DataInterfaceName}_NumCurves;
}

float3 ReadPosition_{DataInterfaceName}(uint VertexIndex)
{
	return ReadControlPointData(VertexIndex).Position;
}

float ReadRadius_{DataInterfaceName}(uint VertexIndex)
{
	return ReadControlPointData(VertexIndex).WorldRadius;
}

float ReadCoordU_{DataInterfaceName}(uint VertexIndex)
{
	return ReadControlPointData(VertexIndex).UCoord;
}

float ReadLength_{DataInterfaceName}(uint VertexIndex)
{
	return ReadControlPointAttributeData(VertexIndex).WorldLength;
}

float2 ReadRootUV_{DataInterfaceName}(uint VertexIndex)
{
	return ReadControlPointAttributeData(VertexIndex).RootUV;
}

float ReadSeed_{DataInterfaceName}(uint VertexIndex)
{
	return ReadControlPointAttributeData(VertexIndex).Seed;
}

float ReadCurveOffsetPoint_{DataInterfaceName}(uint CurveIndex)
{
	return ReadCurveData(CurveIndex).PointIndex;
}

float ReadCurveNumPoint_{DataInterfaceName}(uint CurveIndex)
{
	return ReadCurveData(CurveIndex).PointCount;
}

