// Copyright Epic Games, Inc. All Rights Reserved.

#include "../HairStrands/HairStrandsVoxelPageCommonStruct.ush"

#include "../Common.ush"
#include "../MonteCarlo.ush"
#include "../DeferredShadingCommon.ush"
#include "../LightShaderParameters.ush"
#include "../SceneTextureParameters.ush"
#include "../ScreenSpaceDenoise/SSDPublic.ush"
#include "../SobolRandom.ush"
#include "../TransmissionCommon.ush"
#include "../HairStrands/HairStrandsVisibilityCommon.ush"
#include "RayTracingCommon.ush"
#include "RayTracingDeferredShadingCommon.ush"
#include "RayTracingDirectionalLight.ush"
#include "RayTracingRectLight.ush"
#include "RayTracingSphereLight.ush"
#include "RayTracingSpotLight.ush"
#include "RayTracingPointLight.ush"

/** Light types */
#define LIGHT_TYPE_DIRECTIONAL 0
#define LIGHT_TYPE_POINT 1
#define LIGHT_TYPE_SPOT 2
#define LIGHT_TYPE_RECT 3

#define ShadowMaskType_Opaque 0
#define ShadowMaskType_Hair 1

RaytracingAccelerationStructure TLAS;
RWTexture2D<float4> RWOcclusionMaskUAV;
RWTexture2D<float> RWRayDistanceUAV;

uint ShadowMaskType;
uint LightingChannelMask;
uint SamplesPerPixel;
float NormalBias;
int4 LightScissor;
int2 PixelOffset;

#if USE_HAIR_LIGHTING
#define VOXEL_TRAVERSAL_DEBUG 0
#define VOXEL_TRAVERSAL_TYPE VOXEL_TRAVERSAL_LINEAR_MIPMAP
//#define VOXEL_TRAVERSAL_TYPE VOXEL_TRAVERSAL_LINEAR

#include "../HairStrands/HairStrandsVoxelPageCommon.ush"
#include "../HairStrands/HairStrandsVoxelPageTraversal.ush"

uint				bUseHairVoxel;
Texture2D<uint4>	HairCategorizationTexture;
Texture2D<uint>		HairLightChannelMaskTexture;

float TraverseHair(uint2 PixelCoord, float3 RayOrigin, float3 RayDirection, float InMinT)
{
	float HitT = InMinT;

	// Depth bias
	// Origin is shifted 2 voxels away towards the light + a constant bias of the size of the voxel
	const float3 Random = 0.5;// GetHairVoxelJitter(PixelCoord.xy, View.StateFrameIndexMod8);
	{
		const float MaxVoxelSize = VirtualVoxel.VoxelWorldSize;
		const float3 DepthBias = MaxVoxelSize * (2 * RayDirection + Random);
		RayOrigin += DepthBias;
	}

	#if VOXEL_TRAVERSAL_DEBUG
	const bool bDebugEnabled = PixelCoord.x == GetCursorPos().x && PixelCoord.y == GetCursorPos().y;
	#else
	const bool bDebugEnabled = false;
	#endif

	FHairTraversalResult Result = InitHairTraversalResult();

	const float DistanceThreshold = 1000.0f; 
	const float CoverageThreshold = 0.995f; // When Coverage is high, we do not trace shadow on opaque since hair/fur is covering the background.

	FVirtualVoxelCommonDesc CommonDesc;
	CommonDesc.PageCountResolution = VirtualVoxel.PageCountResolution;
	CommonDesc.VoxelWorldSize = VirtualVoxel.VoxelWorldSize;
	CommonDesc.PageTextureResolution = VirtualVoxel.PageTextureResolution;
	CommonDesc.PageResolution = VirtualVoxel.PageResolution;

	const float3 P0 = RayOrigin;
	const float3 P1 = RayOrigin + RayDirection * (HitT>=0 ? min(HitT, DistanceThreshold) : DistanceThreshold);

	for (uint MacroGroupId = 0; MacroGroupId < VirtualVoxel.NodeDescCount; ++MacroGroupId)
	{
		const FPackedVirtualVoxelNodeDesc PackedNode = VirtualVoxel.NodeDescBuffer[MacroGroupId];
		const FVirtualVoxelNodeDesc NodeDesc = UnpackVoxelNode(PackedNode, VirtualVoxel.PageResolution);

		FHairTraversalSettings TraversalSettings = InitHairTraversalSettings();
		TraversalSettings.DensityScale = VirtualVoxel.DensityScale;
		TraversalSettings.CountThreshold = 1;
		TraversalSettings.DistanceThreshold = DistanceThreshold;
		TraversalSettings.bDebugEnabled = bDebugEnabled;
		TraversalSettings.SteppingScale = VirtualVoxel.SteppingScale;
		TraversalSettings.Random = Random;
		TraversalSettings.bUseOpaqueVisibility = false;

		Result = ComputeHairCountVirtualVoxel(
			P0,
			P1,
			CommonDesc,
			NodeDesc,
			VirtualVoxel.PageIndexBuffer,
			VirtualVoxel.PageTexture,
			TraversalSettings);

		if (Result.HitT >= 0)
		{
			const float WorldHitT = DistanceThreshold * Result.HitT;
			HitT = HitT >= 0 ? min(HitT, WorldHitT) : WorldHitT;
		}
	}

	return HitT;
}
#endif

bool GenerateOcclusionRay(
	FLightShaderParameters LightParameters,
	float3 WorldPosition,
	float3 WorldNormal,
	float2 RandSample,
	out float3 RayOrigin,
	out float3 RayDirection,
	out float RayTMin,
	out float RayTMax
)
{
	#if LIGHT_TYPE == LIGHT_TYPE_DIRECTIONAL
	{
		GenerateDirectionalLightOcclusionRay(
			LightParameters,
			WorldPosition, WorldNormal,
			RandSample,
			/* out */ RayOrigin,
			/* out */ RayDirection,
			/* out */ RayTMin,
			/* out */ RayTMax);
	}
	#elif LIGHT_TYPE == LIGHT_TYPE_POINT
	{
		if (LightParameters.SourceRadius == 0)
		{
			return GeneratePointLightOcclusionRay(
				LightParameters,
				WorldPosition, WorldNormal,
				RandSample,
				/* out */ RayOrigin,
				/* out */ RayDirection,
				/* out */ RayTMin,
				/* out */ RayTMax);
		}
		else
		{
			float RayPdf;
			return GenerateSphereLightOcclusionRay(
				LightParameters,
				WorldPosition, WorldNormal,
				RandSample,
				/* out */ RayOrigin,
				/* out */ RayDirection,
				/* out */ RayTMin,
				/* out */ RayTMax,
				/* out */ RayPdf);
		}
	}
	#elif LIGHT_TYPE == LIGHT_TYPE_SPOT
	{
		return GenerateSpotLightOcclusionRay(
			LightParameters,
			WorldPosition, WorldNormal,
			RandSample,
			/* out */ RayOrigin,
			/* out */ RayDirection,
			/* out */ RayTMin,
			/* out */ RayTMax);
	}
	#elif LIGHT_TYPE == LIGHT_TYPE_RECT
	{
		float RayPdf = 0.0;
		return GenerateRectLightOcclusionRay(
			LightParameters,
			WorldPosition, WorldNormal,
			RandSample,
			/* out */ RayOrigin,
			/* out */ RayDirection,
			/* out */ RayTMin,
			/* out */ RayTMax,
			/* out */ RayPdf);
	}
	#else
		#error Unknown light type.
	#endif
	return true;
}

float3 ComputeDiffuseTransmission(float3 V, float3 P, float3 N, float MeanFreePath, float Eta, FLightShaderParameters LightParameters, float2 LightSample, inout RandomSequence RandSequence, uint RejectionRetryMax)
{
	float TransmissionDistance = SSSS_MAX_TRANSMISSION_PROFILE_DISTANCE;
	float RcpMeanFreePath = rcp(MeanFreePath);

	// Refract inward and force a scattering event
	float3 Tr = refract(V, N, Eta);

	// TODO: decide between stochastic or expectation
	//float ScatterDistance = MeanFreePath;
	uint DummyVariable;
	float RandSample = RandomSequence_GenerateSample1D(RandSequence, DummyVariable);
	float ScatterDistance = -log(RandSample) * MeanFreePath;
	float ScatterDistancePdf = RcpMeanFreePath * exp(-ScatterDistance * RcpMeanFreePath);

	// Reject scatter distances which penetrate through the medium
	const uint RayFlags = 0;
	const uint InstanceInclusionMask = RAY_TRACING_MASK_OPAQUE;

	// Use rejection sampling to improve 1D estimate
	if (RejectionRetryMax > 1)
	{
		RayDesc RejectionTestRay;
		RejectionTestRay.Origin = P;
		RejectionTestRay.Direction = Tr;
		RejectionTestRay.TMin = 0.01;
		RejectionTestRay.TMax = ScatterDistance;
		FMinimalPayload RejectionTestPayload = TraceVisibilityRay(
			TLAS,
			RayFlags,
			InstanceInclusionMask,
			RejectionTestRay);

		float RejectionDistancePdf = ScatterDistancePdf;
		uint RetryCount = 0;
		while (RejectionTestPayload.IsHit() && RetryCount < RejectionRetryMax)
		{
			RetryCount++;
			ScatterDistance /= 2.0;
			// Account for forcibly scattering at a half-distance, given that a previous trial has failed
			ScatterDistancePdf = RcpMeanFreePath * exp(-ScatterDistance * RcpMeanFreePath);
			RejectionDistancePdf = ScatterDistancePdf / RejectionDistancePdf;
			RejectionTestRay.TMax = ScatterDistance;

			RejectionTestPayload = TraceVisibilityRay(
				TLAS,
				RayFlags,
				InstanceInclusionMask,
				RejectionTestRay);
		}
		if (RejectionTestPayload.IsHit()) return 0.0;
		ScatterDistance = ScatterDistance * ScatterDistancePdf / RejectionDistancePdf;
	}

	// Build sub-surface scattering ray
	RayDesc SSSRay;
	SSSRay.Origin = P + Tr * ScatterDistance;
	GenerateOcclusionRay(LightParameters, SSSRay.Origin, N, LightSample, SSSRay.Origin, SSSRay.Direction, SSSRay.TMin, SSSRay.TMax);

	// Clip the ray length by the maximum transmission distance
	float LightTMax = SSSRay.TMax;
	SSSRay.TMax = SSSS_MAX_TRANSMISSION_PROFILE_DISTANCE - ScatterDistance;

	FMinimalPayload SSSPayload = TraceVisibilityRay(
		TLAS,
		RayFlags,
		InstanceInclusionMask,
		SSSRay);

	if (SSSPayload.IsHit())
	{
		// Confirm that there is not an actual occluder beyond the maximum transmission distance
		SSSRay.TMin = SSSPayload.HitT + 0.01;
		SSSRay.TMax = LightTMax;
		FMinimalPayload VisibilityPayload = TraceVisibilityRay(
			TLAS,
			RayFlags,
			InstanceInclusionMask,
			SSSRay);
		if (VisibilityPayload.IsMiss())
		{
			TransmissionDistance = ScatterDistance + SSSPayload.HitT;
		}
	}

	// Conversion to raster model: 1.0 - (distance / SSSS_MAX_TRANSMISSION_PROFILE_DISTANCE)
	return EncodeThickness(TransmissionDistance / SSSS_MAX_TRANSMISSION_PROFILE_DISTANCE);
}

RAY_TRACING_ENTRY_RAYGEN(OcclusionRGS)
{
	uint2 PixelCoord = DispatchRaysIndex().xy + View.ViewRectMin.xy + PixelOffset;

	float Visibility = 0.0;
	float SumRayDistance = 0.0;
	float ClosestRayDistance = DENOISER_INVALID_HIT_DISTANCE;
	float TransmissionDistance = 0.0;
	float HitCount = 0.0;
	float RayCount = 0.0;
	uint LocalSamplesPerPixel = ENABLE_MULTIPLE_SAMPLES_PER_PIXEL ? SamplesPerPixel : 1;

	if (all(PixelCoord >= LightScissor.xy) && all(PixelCoord <= LightScissor.zw))
	{
		RandomSequence RandSequence;
		uint LinearIndex = CalcLinearIndex(PixelCoord);
		RandomSequence_Initialize(RandSequence, LinearIndex, View.StateFrameIndex);

		FLightShaderParameters LightParameters = GetRootLightShaderParameters();

		// Get G-Buffer surface data
		float2 InvBufferSize = View.BufferSizeAndInvSize.zw;
		float2 BufferUV = (float2(PixelCoord) + 0.5) * InvBufferSize;
		uint ShadingModelID = SHADINGMODELID_UNLIT;
		float3 WorldNormal = 0;
		FTransmissionProfileParams TransmissionProfileParams = (FTransmissionProfileParams)0;
		{
#if 0
			FGBufferData GBufferData = GetGBufferDataFromSceneTextures(UV);
#else
			//#dxr-todo: workaround for flickering. UE-87281
			FGBufferData GBufferData = GetGBufferDataFromSceneTexturesLoad(PixelCoord);
#endif
			ShadingModelID = GBufferData.ShadingModelID;
			WorldNormal = GBufferData.WorldNormal;
#if ENABLE_TRANSMISSION
			TransmissionProfileParams = GetTransmissionProfileParams(GBufferData);
#endif
		}

		// Mask out depth values that are infinitely far away

#if 0
		float DeviceZ = SceneDepthBuffer.SampleLevel(SceneDepthBufferSampler, BufferUV, 0).r;
#else
		//#dxr-todo: workaround for flickering. UE-87281
		float DeviceZ = SceneDepthBuffer.Load(int3(PixelCoord, 0)).r;
#endif
		const bool bIsDepthValid = SceneDepthBuffer.Load(int3(PixelCoord, 0)).r > 0.0;
		bool bIsValidPixel = ShadingModelID != SHADINGMODELID_UNLIT && bIsDepthValid;
		uint LightChannel = GetSceneLightingChannel(PixelCoord);

		// Check if there is hair geometry
		uint RaytracingMask = RAY_TRACING_MASK_SHADOW | RAY_TRACING_MASK_THIN_SHADOW;
	#if USE_HAIR_LIGHTING
		if (ShadowMaskType == ShadowMaskType_Hair)
		{
			RaytracingMask = RAY_TRACING_MASK_SHADOW;
			const FCategorizationData Categorization = DecodeCategorizationData(HairCategorizationTexture.Load(uint3(PixelCoord, 0)));
			bIsValidPixel = Categorization.PixelCoverage > 0;
			if (bIsValidPixel)
			{
				DeviceZ = Categorization.ClosestDepth;
				ShadingModelID = SHADINGMODELID_HAIR;
				LightChannel = HairLightChannelMaskTexture.Load(uint3(PixelCoord, 0));
			}
		}
	#endif

		const float3 WorldPosition = ReconstructWorldPositionFromDeviceZ(PixelCoord, DeviceZ);

		const bool bTraceRay = bIsValidPixel && (LightChannel & LightingChannelMask) != 0;
		if (!bTraceRay)
		{
			LocalSamplesPerPixel = 0;
		}

		// Cheap solution for double sided foliage to have valid lighing through the denoiser
		if (ShadingModelID == SHADINGMODELID_TWOSIDED_FOLIAGE)
		{
		#if LIGHT_TYPE == LIGHT_TYPE_DIRECTIONAL
			const float3 LightDirection = LightParameters.Direction;
		#else
			const float3 LightDirection = normalize(LightParameters.Position - WorldPosition);
		#endif
			if (dot(LightDirection, WorldNormal) < 0)
				WorldNormal = -WorldNormal;
		}

		// For hair shading model, WorldNormal is actually the shading tangent.
		// To generate proper bias, we compute a normal oriented toward the light.
		// Normal clipping is removed from hair since the BSDF is spherical, rather 
		// than hemispherical
		//
		// Note: 
		// Since we don't have a notion of backfacing here, there is no correct way 
		// to avoid self-intersection with hair. Either we push towards the light, 
		// (this creates some issue in triangle facing back the light), or we push 
		// toward the view point (this create issue for transmitted light)
		// It seems having proper transmission is more important, thus the light 
		// version is enabled by default
		bool bApplyNormalCulling = true;
		if (ShadingModelID == SHADINGMODELID_HAIR)
		{
#if LIGHT_TYPE == LIGHT_TYPE_DIRECTIONAL
			const float3 LightDirection = LightParameters.Direction;
#else
			const float3 LightDirection = normalize(LightParameters.Position - WorldPosition);
#endif
			WorldNormal = LightDirection;
			bApplyNormalCulling = false;
		}
			
		// Fixes clipping of eye caustic normals in raytrace shadows
		if (ShadingModelID == SHADINGMODELID_EYE)
		{
			bApplyNormalCulling = false;
		}

#if ENABLE_MULTIPLE_SAMPLES_PER_PIXEL
		LOOP for (uint SampleIndex = 0; SampleIndex < LocalSamplesPerPixel; ++SampleIndex)
#else // ENABLE_MULTIPLE_SAMPLES_PER_PIXEL
		do if (bTraceRay)
#endif // ENABLE_MULTIPLE_SAMPLES_PER_PIXEL
		{
			uint DummyVariable;
			float2 RandSample = RandomSequence_GenerateSample2D(RandSequence, DummyVariable);

			#if !ENABLE_MULTIPLE_SAMPLES_PER_PIXEL
			{
				const uint TemporalBits = 10;
				uint FrameIndexMod1024 = ReverseBitsN(GetPowerOfTwoModulatedFrameIndex(1 << TemporalBits), TemporalBits);

				uint2 SobolBase = SobolPixel(PixelCoord);
				uint2 SobolFrame = SobolIndex(SobolBase, FrameIndexMod1024, TemporalBits);

				RandSample = SobolIndexToUniformUnitSquare(SobolFrame);
			}
			#endif // !ENABLE_MULTIPLE_SAMPLES_PER_PIXEL

			RayDesc Ray;
			bool bIsValidRay = GenerateOcclusionRay(
				LightParameters,
				WorldPosition, WorldNormal,
				RandSample,
				/* out */ Ray.Origin,
				/* out */ Ray.Direction,
				/* out */ Ray.TMin,
				/* out */ Ray.TMax);
			ApplyCameraRelativeDepthBias(Ray, PixelCoord, DeviceZ, WorldNormal, NormalBias);

			BRANCH
			if (!bIsValidRay)
			{
				continue;
			}
			else if (bApplyNormalCulling && dot(WorldNormal, Ray.Direction) <= 0.0)
			{
				ClosestRayDistance = 0.001;
				continue;
			}

			// Attenuation check
			if (LightParameters.InvRadius > 0.0)
			{
				float MaxAttenuationDistance = 1.0 / LightParameters.InvRadius;
				if (Ray.TMax > MaxAttenuationDistance)
				{
					continue;
				}
			}

			uint RayFlags = 0;

#if !ENABLE_TWO_SIDED_GEOMETRY
			RayFlags |= RAY_FLAG_CULL_BACK_FACING_TRIANGLES;
#endif

			if (DIM_DENOISER_OUTPUT == 0)
			{
				// Denoiser mode 0 doesn't use depth, so take first hit
				RayFlags |= RAY_FLAG_SKIP_CLOSEST_HIT_SHADER | RAY_FLAG_ACCEPT_FIRST_HIT_AND_END_SEARCH;
			}

			FMinimalPayload MinimalPayload = TraceVisibilityRay(
				TLAS,
				RayFlags,
				RaytracingMask,
				Ray);

			#if USE_HAIR_LIGHTING
			if (ShadowMaskType != ShadowMaskType_Hair && bUseHairVoxel)
			{
				MinimalPayload.HitT = TraverseHair(PixelCoord, Ray.Origin, Ray.Direction, MinimalPayload.HitT);
			}
			#endif

			RayCount += 1.0;
			Visibility += MinimalPayload.IsMiss() ? 1.0 : 0.0;
			if (MinimalPayload.IsHit())
			{
				ClosestRayDistance =
					(ClosestRayDistance == DENOISER_INVALID_HIT_DISTANCE) ||
					(MinimalPayload.HitT < ClosestRayDistance) ? MinimalPayload.HitT : ClosestRayDistance;
				SumRayDistance += MinimalPayload.HitT;
				HitCount += 1.0;
			}
			else
			{
				ClosestRayDistance = (ClosestRayDistance == DENOISER_INVALID_HIT_DISTANCE) ? DENOISER_MISS_HIT_DISTANCE : ClosestRayDistance;
			}
		}
#if !ENABLE_MULTIPLE_SAMPLES_PER_PIXEL
		while (0);
#endif //!ENABLE_MULTIPLE_SAMPLES_PER_PIXEL
		if (ENABLE_TRANSMISSION && bTraceRay && ShadingModelID == SHADINGMODELID_SUBSURFACE_PROFILE)
		{
			// Snag two variables to fix light sampling for all computations..
			uint DummyVariable;
			float2 LightSample = RandomSequence_GenerateSample2D(RandSequence, DummyVariable);

			float3 V = normalize(WorldPosition - View.WorldViewOrigin);
			float MeanFreePath = TransmissionProfileParams.ExtinctionScale;
			float Eta = TransmissionProfileParams.OneOverIOR;
			uint RejectionRetryMax = LocalSamplesPerPixel - 1;
			TransmissionDistance = ComputeDiffuseTransmission(V, WorldPosition, WorldNormal, MeanFreePath, Eta, LightParameters, LightSample, RandSequence, RejectionRetryMax);
		}
	}

	float Shadow = (LocalSamplesPerPixel > 0) ? Visibility / LocalSamplesPerPixel : Visibility;
	
	if (DIM_DENOISER_OUTPUT == 2)
	{
		RWOcclusionMaskUAV[PixelCoord] = float4(
			Shadow,
			ClosestRayDistance,
			0,
			TransmissionDistance);
	}
	else if (DIM_DENOISER_OUTPUT == 1)
	{
		float AvgHitDistance = -1.0;
		if (HitCount > 0.0)
		{
			AvgHitDistance = SumRayDistance / HitCount;
		}
		else if (RayCount > 0.0)
		{
			AvgHitDistance = 1.0e27;
		}

		// TODO(Denoiser): the denoiser would much prefer a single RG texture.
		RWOcclusionMaskUAV[PixelCoord] = float4(Shadow, TransmissionDistance, Shadow, TransmissionDistance);
		RWRayDistanceUAV[PixelCoord] = AvgHitDistance;
	}
	else
	{
		const float ShadowFadeFraction = 1;
		float SSSTransmission = TransmissionDistance;

		// 0 is shadowed, 1 is unshadowed
		// RETURN_COLOR not needed unless writing to SceneColor;
		float FadedShadow = lerp(1.0f, Square(Shadow), ShadowFadeFraction);
		float FadedSSSShadow = lerp(1.0f, Square(SSSTransmission), ShadowFadeFraction);

		// the channel assignment is documented in ShadowRendering.cpp (look for Light Attenuation channel assignment)
		float4 OutColor = EncodeLightAttenuation(half4(FadedShadow, FadedSSSShadow, FadedShadow, FadedSSSShadow));

		RWOcclusionMaskUAV[PixelCoord] = OutColor;
	}
}
